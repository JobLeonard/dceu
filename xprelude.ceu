;; is', isnot'

func is' (v1,v2) {
    ifs {
        (v1 == v2)         -> true
        (type(v2) /= :tag) -> false
        (type(v1) == v2)   -> true
        tags(v1,v2)        -> true
        else -> false
    }
}

func isnot' (v1,v2) {
    not is'(v1,v2)
}

;; ARITHMETIC:  +  -  *  /  %  **  //

func {+} (v1, v2) {
    if (v1 isnot :number) or (v2 isnot :number) {
        throw(:error)
    }
    `:number ($v1.Number + $v2.Number)`
}

func {-} (v1, v2) {
    ifs {
        (v1 is :number) and (v2 is :nil) -> {
            `:number (- $v1.Number)`
        }
        (v1 isnot :number) or (v2 isnot :number) -> {
            throw(:error)
        }
        else -> {
            `:number ($v1.Number - $v2.Number)`
        }
    }
}

func {*} (v1, v2) {
    if (v1 isnot :number) or (v2 isnot :number) {
        throw(:error)
    }
    `:number ($v1.Number * $v2.Number)`
}

func {**} (v1, v2) {
    if (v1 isnot :number) or (v2 isnot :number) {
        throw(:error)
    }
    `:number powf($v1.Number, $v2.Number)`
}

func {/} (v1, v2) {
    if (v1 isnot :number) or (v2 isnot :number) {
        throw(:error)
    }
    `:number ($v1.Number / $v2.Number)`
}

func {//} (v1, v2) {
    if (v1 isnot :number) or (v2 isnot :number) {
        throw(:error)
    }
    `:number powf($v1.Number, 1/$v2.Number)`
}

func {%} (v1, v2) {
    if (v1 isnot :number) or (v2 isnot :number) {
        throw(:error)
    }
    `:number fmod($v1.Number, $v2.Number)`
}

;; RELATIONAL:  >=  >  <=  <

func {>} (v1,v2) {
    ifs {
        (v1 is :tag)    and (v2 is :tag)    -> `:bool ($v1.Tag    > $v2.Tag)`
        (v1 is :number) and (v2 is :number) -> `:bool ($v1.Number > $v2.Number)`
        else -> throw(:error)
    }
}

func {<} (v1,v2) {
    not ((v1 == v2) or (v1 > v2))
}

func {>=} (v1,v2) {
    (v1 == v2) or (v1 > v2)
}

func {<=} (v1,v2) {
    (v1 == v2) or (v1 < v2)
}

;; max, min

func min (v1, v2) {
    ((v1 < v2) and v1) or v2
}

func max (v1, v2) {
    ((v1 > v2) and v1) or v2
}

;; tostring, tonumber, assert

func ctostring (v) {
    assert(type(v) == :pointer)
    var i = 0
    var n = `:number strlen($v.Pointer)`
    var str = #[]
    while i < n {
        set str[#str] = `:char ((char*)$v.Pointer)[(int)$i.Number]`
        set i = i + 1
    }
    move(str)
}

func tostring (v) {
    ifs v {
        is :pointer -> ctostring(v)
        is :tag -> ctostring(`:pointer ceu_tag_to_string($v.Tag)`)
        is :number -> {
            ```
            static char str[255];
            snprintf(str, 255, "%g", $v.Number);
            ```
            tostring(`:pointer str`)
        }
        else -> v   ;; TODO
    }
}

func tonumber (v) {
    ifs v {
        ((v is :vector) and (#v > 0)) and (v[0] is :char) -> {
            `:number atoi($v.Dyn->Vector.mem)`
        }
        else -> v   ;; TODO
    }
}

func assert (v, msg) {
    if not v {
        print("assertion error : ")
        println(msg or "no reason given")
        throw(:assert)
    }
}

;; (++) concat

func {<++} (v1, v2) {
    assert(type(v1) == :vector)
    assert(type(v2) == :vector)
    do {
        var i = 0
        while i < #v2 {
            set v1[#v1] = v2[i]
            set i = i + 1
        }
    }
    v1
}

func {++} (v1, v2) {
    (#[] <++ v1) <++ v2
}

;; tovector

func tovector (v) {
    ifs v {
        is :coro -> {
            var ret = #[]
            while in :coro, v, vv {
                set ret[#ret] = vv
            }
            move(ret)
        }
        else -> throw(:TODO)
    }
}